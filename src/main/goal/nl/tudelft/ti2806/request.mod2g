use serviceKnowledge as knowledge.
use service as actionspec.

module request {
	
	% description: if we get an information request answer with OK.
	% if:
	% - you have an information request
	% - it is possible to answer with OK 
	% then:
	% - answer the request with OK.	
	% - remove the request from the belief base
	if
		bel(
			request(Type, RequestType, RequestID, ContentLinkID, VisibleStakeholders, ActionlogIDs, Price, Multipolygon, Area, Answers),
			Type == 'INFORMATION', 
			member(answer(AnswerID, 'OK'), Answers)
		)
	then 
		popup_answer(RequestID, AnswerID) +
		delete(request(Type, RequestType, RequestID, ContentLinkID, VisibleStakeholders, ActionlogIDs, Price, Multipolygon, Area, Answers)).
	
	% description: if we get an interaction request to buy land, answer with No or OK. 
	%				You can only have an OK option if there are no other options.
	% if:
	% - you have an interaction request
	% - this request is to buy some land
	% - it is possible to answer with No or OK 
	% then:
	% - answer the request with No or OK.
	% - remove the request from the belief base
	if
		bel( 
			request(Type, RequestType, RequestID, ContentLinkID, VisibleStakeholders, ActionlogIDs, Price, Multipolygon, Area, Answers),
			Type == 'INTERACTION', 
			RequestType = 'BUY_LAND',
			(member(answer(AnswerID, 'Nee'), Answers); member(answer(AnswerID, 'OK'), Answers))
			% TODO: decide if we want to sell this piece of land
			% based on the price per m^2, zone, service buildings present, buildable land and indicator change
		)
	then 
		popup_answer(RequestID, AnswerID) +
		delete(request(Type, RequestType, RequestID, ContentLinkID, VisibleStakeholders, ActionlogIDs, Price, Multipolygon, Area, Answers)).

	% description: if we get an interaction request to sell land, answer with Yes or OK. 
	%				You can only have an OK option if there are no other options.
	% if:
	% - you have an interaction request
	% - this request is to sell some land
	% - it is possible to answer with Yes or OK 
	% - the price per m^2 is below or equal to the price threshold
	% then:
	% - answer the request with Yes or OK.
	% - remove the request from the belief base
	if
		bel(
			request(Type, RequestType, RequestID, ContentLinkID, VisibleStakeholders, ActionlogIDs, Price, Multipolygon, Area, Answers),
			Type == 'INTERACTION', 
			RequestType = 'SELL_LAND',
			(member(answer(AnswerID, 'Ja'), Answers); member(answer(AnswerID, 'OK'), Answers)),
			marktprijs(Huidigemarktprijs),
			(Huidigemarktprijs >= Price/Area)
			% TODO: decide if we want to accept the offer to buy this piece of land
			% based on the price per m^2, zone and amount of buildings present/buildable land
		)
	then 
		popup_answer(RequestID, AnswerID) +
		delete(request(Type, RequestType, RequestID, ContentLinkID, VisibleStakeholders, ActionlogIDs, Price, Multipolygon, Area, Answers)).
	
	% description: if we get an interaction request to demolish, answer with Yes or OK. 
	%				You can only have an OK option if there are no other options.
	% if:
	% - you have an interaction request
	% - this request is to demolish
	% - it is possible to answer with Yes or OK 
	% then:
	% - answer the request with Yes or OK.
	% - remove the request from the belief base
	if
		bel(
			request(Type, RequestType, RequestID, ContentLinkID, VisibleStakeholders, ActionlogIDs, Price, Multipolygon, Area, Answers),
			Type == 'INTERACTION', 
			RequestType = 'DEMOLISH',
			(member(answer(AnswerID, 'Ja'), Answers); member(answer(AnswerID, 'OK'), Answers))
			% TODO: decide if the indicator change and cost of demolishing is worth it.
		)
	then 
		popup_answer(RequestID, AnswerID) +
		delete(request(Type, RequestType, RequestID, ContentLinkID, VisibleStakeholders, ActionlogIDs, Price, Multipolygon, Area, Answers)).

	% description: if we get an stakeholder_standard request remove it from the belief base. 
	%				Also add request_answered to suppress new handling.
	% if:
	% - you have a request
	% - with type is 'STAKEHOLDER_STANDARD'
	% then:
	% - set the request ID to answered
	% - remove the request from the belief base
	if
		bel(
			request(Type, RequestType, RequestID, ContentLinkID, VisibleStakeholders, ActionlogIDs, Price, Multipolygon, Area, Answers),
			Type == 'STAKEHOLDER_STANDARD'
		)
	then 
		insert(request_answered(RequestID)) +
		delete(request(Type, RequestType, RequestID, ContentLinkID, VisibleStakeholders, ActionlogIDs, Price, Multipolygon, Area, Answers)).
}
