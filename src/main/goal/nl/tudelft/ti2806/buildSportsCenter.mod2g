use service as actionspec.
use serviceKnowledge as knowledge.

exit = always.

% buildSportsCenter:
% - states: 1
% - description: Builds a Sports Center if possible
module buildSportsCenter {
	% description: Build a sports center on a specified MultiPolygon.
	% if:
	% - there is a building type 'sports center' or 'sportcentrum'
	% - we have an indicator who whant to build this type
	% - The zoneID is the zone with the highest needed value.
	% - Size is sqrt of the total needed value.
	% - There is no land that is big enough to build on.
	% then:
	% - Build the sports center on the given zone, with the given size 10 meters next to a road
	% - update the counter.
	if 
		bel(
			functions(Functions), 
			member([Name,FunctionID,_],Functions), 
			(Name =='sportcentrum'; Name == 'sports center'),
			my_stakeholder_id(StakeholderID),
			indicatorLink(_, IndicatorWeights), 
			member(indicatorWeights(IndicatorID, IndicatorName, _), IndicatorWeights), 
			(IndicatorName == 'Sportcentrum Indicator'; IndicatorName == 'Sportcentrum Indicator (2)'), 
			indicator(IndicatorID, _, _, ZoneList),
			maxTodo(ZoneList, Todo, ZoneID), 
			Size is sqrt(Todo),
			counter(ZoneID, Count), 
			NewCount is Count + 1
		)
	then 
		build_specified_building(StakeholderID, ZoneID, FunctionID, Size, Size, 10) +
		delete(counter(ZoneID, Count)) +
		insert(counter(ZoneID, NewCount)).
}
